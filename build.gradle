plugins {
  id "java-library"
  id "net.minecrell.licenser" version "0.4.1"
  id "com.github.johnrengelman.shadow" version "5.2.0"
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

group = "com.mineteria"
version = "0.1.1"
description = "Allows the application of mixins provided by plugins on Mineteria."

repositories {
  mavenCentral()
  maven {
    url "https://oss.sonatype.org/content/repositories/snapshots/"
  }
  maven {
    url "https://repo.spongepowered.org/maven/"
  }
  maven {
    url "https://files.minecraftforge.net/maven/"
  }
}

dependencies {
  // API
  api "org.checkerframework:checker-qual:3.9.1"
  api "net.minecrell:terminalconsoleappender:1.1.1"

  // Common
  api("com.google.guava:guava:21.0") {
    exclude group: "com.google.code.findbugs", module: "jsr305" // We don't want to use jsr305, use checkerframework
    exclude group: "org.checkerframework", module: "checker-qual" // We use our own version
    exclude group: "com.google.j2objc", module: "j2objc-annotations"
    exclude group: "org.codehaus.mojo", module: "animal-sniffer-annotations"
    exclude group: "com.google.errorprone", module: "error_prone_annotations"
  }

  api "com.google.code.gson:gson:2.8.0"

  // Mixins
  api "org.spongepowered:mixin:0.8.2"

  // ASM
  api "org.ow2.asm:asm:7.2"
  api "org.ow2.asm:asm-analysis:7.2"
  api "org.ow2.asm:asm-commons:7.2"
  api "org.ow2.asm:asm-tree:7.2"
  api "org.ow2.asm:asm-util:7.2"

  // Access Transformers
  api("net.minecraftforge:accesstransformers:2.2.1:service") {
    exclude group: "org.apache.logging.log4j"
  }

  // Core
  compile("cpw.mods:modlauncher:8.0.9") {
    exclude group: "org.apache.logging.log4j"
  }

  compile("cpw.mods:grossjava9hacks:1.3.0") {
    exclude group: "org.apache.logging.log4j"
  }
}

license {
  header = file("header.txt")
  newLine = false
  ext {
    name = "Implant"
    organization = "Mineteria"
    url = "https://mineteria.com/"
  }

  include("**/*.java", "**/*.groovy", "**/*.kt")
}

tasks.withType(JavaCompile) {
  options.compilerArgs += ["-Xlint:all", "-Xlint:-path", "-parameters"]
  options.deprecation = true
  options.encoding = "UTF-8"
}


jar {
  manifest {
    attributes "Main-Class": "com.mineteria.implant.ImplantBootstrap"
    attributes "Premain-Class": "com.mineteria.implant.agent.Agent"

    attributes([
      "Specification-Title": "Implant",
      "Specification-Vendor": "Mineteria",
      "Specification-Version": 1.0,
      "Implementation-Title": project.name,
      "Implementation-Version": project.version,
      "Implementation-Vendor": "Mineteria"
    ], "com/mineteria/implant/")

    from { zipTree { configurations.compile.files.find { entry -> entry.name.contains("modlauncher") } }.matching { include "**/MANIFEST.MF" }.singleFile }
  }
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allJava
  classifier = 'sources'
}

shadowJar {
  classifier = "shaded"

  transform(com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer)

  dependencies {
    // API
    include dependency('org.apache.logging.log4j:log4j-api')
    include dependency('org.apache.logging.log4j:log4j-core')
    include dependency('org.checkerframework:checker-qual')
    include dependency('net.minecrell:terminalconsoleappender')
    include dependency('org.jline:jline-reader')
    include dependency('org.jline:jline-terminal')
    include dependency('org.jline:jline-terminal-jna')
    include dependency('net.java.dev.jna:jna')

    // Common
    include dependency('com.google.guava:guava')
    include dependency('com.google.code.gson:gson')

    // ASM
    include dependency('org.ow2.asm:asm')
    include dependency('org.ow2.asm:asm-analysis')
    include dependency('org.ow2.asm:asm-commons')
    include dependency('org.ow2.asm:asm-tree')
    include dependency('org.ow2.asm:asm-util')

    // Mixin
    include dependency('org.spongepowered:mixin')

    // Access Transformers
    include dependency('net.minecraftforge:accesstransformers')
    include dependency('org.antlr:antlr4-runtime')

    // Core
    include dependency('cpw.mods:modlauncher')
    include dependency('cpw.mods:grossjava9hacks')
    include dependency('net.sf.jopt-simple:jopt-simple')
  }

  relocate 'com.google', 'com.mineteria.implant.relocate.google'
}
